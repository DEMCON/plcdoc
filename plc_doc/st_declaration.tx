// Process complete declaration
Declaration:
    typedefs*=TypeDef
    (function=Function)?
;

TypeDef:
    comment=CommentBlock?
    'TYPE' name=ID ('EXTENDS' extends=ID)? ':'
    (type=TypeStruct | type=TypeEnum)
    'END_TYPE'
    CommentIgnored*
;

TypeStruct:
    'STRUCT'
    properties*=Variable
    'END_STRUCT'
;

TypeEnum:
    '('
    values*=EnumOption
    ');'
;

EnumOption:
    name=ID
    (':=' INT)?
    ','?
;

Function:
    comment=CommentBlock?
    function_type=FunctionType name=ID ('EXTENDS' extends=ID)? (':' return=ID)?
    lists*=VariableList
    CommentIgnored*
;

FunctionType :
    'FUNCTION_BLOCK' | 'FUNCTION'
;

Listing :
    VariableList | CommentIgnored
;

VariableList:
    CommentIgnored*
    name=VariableListType
    variables*=Variable
    'END_VAR'
    CommentIgnored*
;

VariableListType:
    /VAR_.*/ | 'VAR'
;

// A single variable definition
Variable:
    CommentIgnored*
    name=ID ':' type=ID ';' comment=CommentDescription?
    CommentIgnored*
;

// The `Comment` clause is reserved, create a custom clause that won't be processed
CommentIgnored: /\/\/.*$/;

CommentDescription:
    '//' comment=/.*$/
;

CommentBlock[noskipws]:
    comment=/\(\*(.|\n)*\*\)/
;
